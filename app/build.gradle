apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

ext.java_version = JavaVersion.VERSION_1_8

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.gmail.yaroslavlancelot.technarium"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en', 'uk'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility = java_version
        sourceCompatibility = java_version
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
        test.java.srcDirs += 'src/test/kotlin/'
    }
}

dependencies {
    implementation(
            //kotlin
            "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
            //android
            'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3',
            'androidx.core:core-ktx:1.1.0',
            "androidx.navigation:navigation-fragment-ktx:$navigation_version",
            "androidx.navigation:navigation-ui-ktx:$navigation_version",
            "android.arch.lifecycle:extensions:$android_arch_version",
            "android.arch.lifecycle:viewmodel:$android_arch_version",
            //DB
            "androidx.room:room-runtime:$room_version",
            "androidx.room:room-ktx:$room_version",
            //UI
            "androidx.constraintlayout:constraintlayout:2.0.0-beta4",
            "androidx.recyclerview:recyclerview:1.1.0",
            'com.google.android.material:material:1.0.0',
            'com.github.ybq:Android-SpinKit:1.4.0',
            'com.makeramen:roundedimageview:2.3.0',
            //network
            'com.squareup.retrofit2:retrofit:2.7.0',
            'com.squareup.retrofit2:converter-simplexml:2.7.0',
            //DI
            'com.google.dagger:dagger-android:2.25.4',
            'com.google.dagger:dagger-android-support:2.25.4',
            //debugging
            'com.jakewharton.timber:timber:4.7.1')
    kapt(
            //DB
            "androidx.room:room-compiler:$room_version",
            //DI
            'com.google.dagger:dagger-compiler:2.25.4',
            'com.google.dagger:dagger-android-processor:2.25.4')
    //debug
    debugImplementation(
            //leaks check
            'com.squareup.leakcanary:leakcanary-android:2.1'
    )
    // tests
    testImplementation(
            //mockito
            "io.mockk:mockk:1.9.3",
            //junit
            'junit:junit:4.13')
    kaptTest(
            //DI
            'com.google.dagger:dagger-compiler:2.25.4')
    androidTestImplementation(
            //junit
            'androidx.test.ext:junit:1.1.1',
            //on device
            'androidx.test.espresso:espresso-core:3.2.0')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = java_version
    }
}